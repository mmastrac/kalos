import file;
import kalos;
import sys;

var exe;
var verbose;

fn help() {
    println("""Usage:
  {exe} [-v] compile idl-file source-file compiled-file
  {exe} [-v] dispatch idl-source-file c-file
  {exe} [-v] dump compiled-file
  {exe} [-v] hexify source-file c-file
  {exe} [-v] idl idl-source-file idl-file
  {exe} [-v] run source-file
  {exe} [-v] stringify source-file c-file""");
    sys.exit(1);
}

fn extract_args(args): object {
    if len(args) <= 1 {
        help();
    }
    # Extract the last path segment
    var segment;
    for segment in split(args[0], "/") {
        exe = segment;
    }
    args = tail(args);
    if len(args) > 0 && args[0] == "-v" {
        verbose = 1;
        args = tail(args);
    }
    return args;
}

fn open_read(f): object {
    if f == "-" {
        return file.stdin;
    }
    return file.open(f, file.READ);
}

fn open_write(f): object {
    if f == "-" {
        return file.stdout;
    }
    return file.open(f, file.WRITE | file.CREATE);
}

fn go_compile(args) {
    if len(args) != 3 {
        help();
    }
    var idl = file.read(open_read(args[0]));
    var compiled_idl = kalos.compile_idl(idl);
    var script = file.read(open_read(args[1]));
    var compiled_script = kalos.compile_script(compiled_idl, script);
    file.write(open_write(args[2]), compiled_script.data);
    # var i;
    # for i in range(0, len(compiled.data)) {
    #     var c = compiled.data[i];
    #     print("0x{ord(c):02x},"); # see note about zero-pad in test_string_formatting
    # }
}

fn go_dispatch(args) {
    if len(args) != 2 {
        help();
    }

    # First, compile the IDL provided by the user
    var idl = file.read(open_read(args[0]));
    var compiled_idl = kalos.compile_idl(idl);
    kalos.generate_dispatch(compiled_idl);

    # Compile our IDL dispatch compiler
#    var self_idl = kalos.compile_idl(kalos.idl);
#    var dispatch_script = kalos.compile_script(self_idl, kalos.idl_script);
}

fn go_dump(args) {
    if len(args) != 1 {
        help();
    }
    var f = open_read(args[0]);
    var script = file.read(f);
}

fn go_hexify(args) {
    if len(args) != 2 {
        help();
    }
    var data = file.read(open_read(args[0]));
    var out = open_write(args[1]);
    var i;
    for i in range(0, len(data)) {
        if i && i % 16 == 0 {
            file.write(out, "\n");
        }
        var c = data[i];
        file.write(out, "0x{ord(c):02x},"); # see note about zero-pad in test_string_formatting
    }
}

fn go_idl(args) {
    if len(args) != 2 {
        help();
    }
    var idl = file.read(open_read(args[0]));
    var compiled_idl = kalos.compile_idl(idl);
    file.write(open_write(args[1]), compiled_idl.data);
}

fn go_run(args) {
    if len(args) != 1 {
        help();
    }
    var idl = kalos.idl;
    var compiled_idl = kalos.compile_idl(idl);
    var script = file.read(open_read(args[0]));
    var compiled_script = kalos.compile_script(compiled_idl, script);
    kalos.run_script(compiled_script, tail(args));
}

fn go_stringify(args) {
    if len(args) != 2 {
        help();
    }
    var data = file.read(open_read(args[0]));
    var out = open_write(args[1]);
    var i;
    file.write(out, "\"");
    for i in range(0, len(data)) {
        var c = data[i];
        if c == "\n" {
            file.write(out, "\\n\"\n\"");
        } else if c == "\"" {
            file.write(out, "\\\"");
        } else if ord(c) < 32 || ord(c) >= 127 {
            file.write(out, "\\x{ord(c):02x}");
        } else {
            file.write(out, c);
        }
    }
    file.write(out, "\"\n");
}

on sys.main(args) {
    args = extract_args(args);
    if len(args) == 0 {
        help();
    }
    if (args[0] == "compile") {
        go_compile(tail(args));
    } else if (args[0] == "dispatch") {
        go_dispatch(tail(args));
    } else if (args[0] == "dump") {
        go_dump(tail(args));
    } else if (args[0] == "hexify") {
        go_hexify(tail(args));
    } else if (args[0] == "idl") {
        go_idl(tail(args));
    } else if (args[0] == "run") {
        go_run(tail(args));
    } else if (args[0] == "stringify") {
        go_stringify(tail(args));
    } else {
        help();
    }
}
