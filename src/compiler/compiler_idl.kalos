# IDL parser, making use of our lexer service. Constructs a list of list (of lists, etc) that
# contains the information from the IDL file in structured form. It then maps this to an
# object with appropriate properties that can be used to walk the structure of the IDL.

var lexer;
var prop_ids;

fn idl_error(s) {
    print("ERROR: {s}\n");
    sys.exit(1);
}

fn idl_log(s) {
    # print("LOG: {s}\n");
}

fn expect_word(): string {
    var next = lexer.read;
    if next.token == "WORD" {
        idl_log("    token = {next.token} string = {next.string}");
        return next.string;
    }
    if next.is_word {
        return next.string;
    }
    idl_error("Expected WORD, but got token {next.token}");
    return "<error>";
}

fn expect_token(token): object {
    var next = lexer.read;
    if next.token != token {
        idl_error("Expected {token} but got {next.token}");
    }
    return next;
}

# Allocate the next given property invoke ID
fn idl_allocate_prop_id(name): number {
    var prop_index = 0;
    var prop;
    for prop in prop_ids {
        if prop == name {
            return prop_index + 1;
        }
        prop_index += 1;
    }
    append(prop_ids, name);
    return len(prop_ids);
}

fn parse_idl_type(): object {
    if lexer.peek.token == "COLON" {
        expect_token("COLON");
        var type = lexer.read.string;
        idl_log("  type: {type}");
        return type;
    }
    return "void";
}

fn parse_idl_args(): object {
    var args = [];
    expect_token("PAREN_OPEN");
    if lexer.peek.token != "PAREN_CLOSE" {
        loop {
            var param_name = expect_word();
            var param_type = parse_idl_type();
            if lexer.peek.token == "ELLIPSIS" {
                expect_token("ELLIPSIS");
                append(args, [param_name, param_type, true]);
            } else {
                append(args, [param_name, param_type, false]);
            }
            if lexer.peek.token == "PAREN_CLOSE" {
                break;
            }
            expect_token("COMMA");
        }
    }
    expect_token("PAREN_CLOSE");
    return args;
}

fn parse_idl_binding(invoke_id): object {
    expect_token("EQ");
    var retval;
    if lexer.peek.token == "C" {
        expect_token("C");
        var token = expect_token("STRING_LITERAL");
        retval = ["c", token.string, invoke_id];
    } else {
        var word = expect_word();
        retval = ["fn", word, invoke_id];
    }
    expect_token("SEMI");
    idl_log("  binding: {retval}");
    return retval;
}

fn parse_idl_prop(invoke_id): object {
    var prop_name = expect_word();
    if invoke_id == -1 {
        invoke_id = idl_allocate_prop_id(prop_name);
    }
    var type = parse_idl_type();
    expect_token("BRA_OPEN");
    var token = lexer.read;
    var read = ["none"];
    var write = ["none"];
    if token.token == "READ" {
        read = parse_idl_binding(invoke_id);
        token = lexer.read;
    }
    if token.token == "WRITE" {
        write = parse_idl_binding(invoke_id);
        token = lexer.read;
    }
    if token.token != "BRA_CLOSE" {
        idl_error("Expected BRA_CLOSE but got {token.token}");
    }
    return ["prop", prop_name, type, read, write];
}

fn parse_idl_fn(invoke_id): object {
    var fn_name = expect_word();
    var bindings = [];
    var args;
    var binding;
    var return_type;
    if lexer.peek.token == "BRA_OPEN" {
        expect_token("BRA_OPEN");
        loop {
            if lexer.peek.token == "BRA_CLOSE" {
                break;
            }
            args = parse_idl_args();
            return_type = parse_idl_type();
            binding = parse_idl_binding(invoke_id);
            append(bindings, [args, return_type, binding]);
        }
        expect_token("BRA_CLOSE");
    } else {
        args = parse_idl_args();
        return_type = parse_idl_type();
        binding = parse_idl_binding(invoke_id);
        append(bindings, [args, return_type, binding]);
    }
    var fn_list = ["fn", fn_name, bindings];
    idl_log("fn: {fn_list}");
    return fn_list;
}

fn parse_idl_handler(index): object {
    var name = expect_word();
    var args = [];
    if lexer.peek.token == "PAREN_OPEN" {
        expect_token("PAREN_OPEN");
        if lexer.peek.token != "PAREN_CLOSE" {
            loop {
                var param_name = expect_word();
                var param_type = parse_idl_type();
                if lexer.peek.token == "ELLIPSIS" {
                    expect_token("ELLIPSIS");
                    append(args, [param_name, param_type, true]);
                } else {
                    append(args, [param_name, param_type, false]);
                }
                if lexer.peek.token == "PAREN_CLOSE" {
                    break;
                }
                expect_token("COMMA");
            }
        }
        expect_token("PAREN_CLOSE");
    }
    var return_type = parse_idl_type();
    expect_token("SEMI");
    var handler_list = ["handler", index, name, return_type, args];
    idl_log("handler: {handler_list}");
    return handler_list;
}

fn parse_idl_const(): object {
    var name = expect_word();
    var const_type = parse_idl_type();
    expect_token("EQ");
    var value = lexer.read.string;
    expect_token("SEMI");
    var const_list = ["const", name, const_type, value];
    idl_log("const: {const_list}");
    return const_list;
}

fn parse_idl_object(): object {
    var name = expect_word();
    var items = [];
    expect_token("BRA_OPEN");
    loop {
        if lexer.peek.token == "BRA_CLOSE" {
            break;
        }
        var token = lexer.read;
        if token.token == "PROP" {
            var prop = parse_idl_prop(-1);
            append(items, prop);
        } else {
            idl_error("Unexpected token in idl object block: {token.token}");
        }
    }
    expect_token("BRA_CLOSE");
    return ["object", name, items];
}

fn parse_idl_module(index): object {
    var name = expect_word();
    var module = [];
    var handler_index = 1;
    var function_index = 1;
    expect_token("BRA_OPEN");
    loop {
        var token = lexer.read;
        var item;
        if token.token == "BRA_CLOSE" {
            break;
        } else if token.token == "FN" {
            item = parse_idl_fn(function_index);
            function_index += 1;
            append(module, item);
        } else if token.token == "PROP" {
            item = parse_idl_prop(function_index);
            function_index += 1;
            append(module, item);
        } else if token.token == "HANDLER" {
            item = parse_idl_handler(handler_index);
            handler_index += 1;
            append(module, item);
        } else if token.token == "CONST" {
            item = parse_idl_const();
            append(module, item);
        } else if token.token == "OBJECT" {
            item = parse_idl_object();
            # TODO: We shouldn't allocate invoke IDs for object props
            function_index += len(item[2]);
            append(module, item);
        } else {
            idl_error("Unexpected token in idl module block: {token.token}");
        }
    }
    return ["module", index, name, module];
}

fn parse_idl_block(): object {
    var prefix = "";
    var dispatch = "";
    var modules = [];
    var index = 0;

    loop {
        if lexer.peek.token == "BRA_CLOSE" {
            break;
        }
        var token = lexer.read;
        if token.token == "PREFIX" {
            token = expect_token("STRING_LITERAL");
            prefix = token.string;
            expect_token("SEMI");
        } else if token.token == "DISPATCH" {
            token = expect_token("NAME");
            dispatch = "name";
            expect_token("SEMI");
        } else if token.token == "MODULE" {
            var module = parse_idl_module(index);
            index += 1;
            append(modules, module);
        } else {
            idl_error("Unexpected token in idl block: {token.token}");
        }
    }

    return ["idl", prefix, dispatch, modules];
}

export fn parse_idl(lexer_): object {
    lexer = lexer_;
    prop_ids = [];
    expect_token("IDL");
    expect_token("BRA_OPEN");
    var idl = parse_idl_block();
    # print("{idl}");
    idl = kidl.create_list_object("idl", idl);
    expect_token("BRA_CLOSE");
    lexer = 0;
    return idl;
}
