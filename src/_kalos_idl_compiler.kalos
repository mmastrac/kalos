import module;
import kidl;

var in_object = false;
var in_module = false;
var is_dynamic = false;
var dispatch = "";
var dynamic_dispatch = "";
var current_module;

const FORWARD = "forward";  # Forward declaration phase
const IMPL = "impl";        # Implementation phase
const HANDLE = "handler";
const DISPATCH = "dispatch";
const OBJECT = "object";
const TYPES = [
    ["string",  "STRING",   "kalos_string", "*",      "kalos_string_release((kalos_state*)state, x);"],
    ["number",  "NUMBER",   "kalos_int", "",          ";"],
    ["bool",    "BOOL",     "kalos_int", "",          ";"],
    ["object",  "OBJECT",   "kalos_object_ref", "*",  "kalos_object_release((kalos_state*)state, &x);"],
    ["any",     "ANY",      "kalos_value", "*",       "kalos_clear((kalos_state*)state, x);"],
];

fn write_matcher(id, name, arg_count) {
    if is_dynamic {
        println("\tif (K__name({arg_count}, \"{name}\")) {{");
    } else {
        println("\tcase {id}: {{");
    }
}

fn write_binding(binding, arg_types, vararg_type) {
    var index = 0;
    var arg;
    if binding.type == "c" {
        var c = binding.value;
        for arg in arg_types {
            c = replace(c, "@" + index, "(K__arg_{arg}(K__arg(arg_count, {index+1})))");
            index += 1;
        }
        print("({c})");
    } else {
        print("{binding.value}((void*)state");
        if (in_object) {
            print(", object");
        }
        for arg in arg_types {
            print(", K__arg_{arg}(K__arg(arg_count, {index+1}))");
            index += 1;
        }
        if vararg_type != "void" {
            print(", vararg_count, kalos_stack_vararg_start(stack, vararg_count)");
        }
        print(")");
    }
}

on open(dynamic) {
    is_dynamic = dynamic;
    println("/* Generated by kalos_idl_compiler - DO NOT MODIFY */");
    println("");
    var type;
    for type in TYPES {
        println("#define K__p{type[0]:<6} {type[2]}{type[3]}");
        println("#define K__{type[0]:<7} {type[2]}");
    }
    println("");
    for type in TYPES {
        println("#define K__t_{type[0]:<6} KALOS_VALUE_{type[1]}");
    }
    println("");
    for type in TYPES {
        println("#define K__clean_{type[0]:<6}(stack, x) {type[4]}");
    }
    println("#define K__arg(n, x)     peek(stack, (n)-(x))");
    println("#define K__arg_string(x) &((x)->value.string)");
    println("#define K__arg_number(x) (x)->value.number");
    println("#define K__arg_bool(x)   (x)->value.number");
    println("#define K__arg_object(x) &((x)->value.object)");
    println("#define K__arg_any(x)    (x)");
    if is_dynamic {
        println("#define K__name(n, s) (param_count == n && strcmp(name, s) == 0)");
    }
    println("");
    kidl.walk_modules(FORWARD);
    kidl.walk_modules(IMPL);
}

on close {
    if is_dynamic {
        println("bool {kidl.prefix}dynamic_dispatch(kalos_run_state* state, const char* module, const char* name, int param_count, kalos_stack* stack, bool retval) {{");
        println(dynamic_dispatch);
        println("\tLOG(\"Module %s not found\", module);");
        println("\treturn false;");
        println("}}");
        println("");
    } else {
        println("kalos_dispatch_fn {kidl.prefix}dispatch[] = {{");
        print(dispatch);
        println("}};");
        println("");
    }
    println("#undef K__string");
    println("#undef K__number");
    println("#undef K__bool");
    println("#undef K__object");
    println("#undef K__any");
}

on module.begin(context, m) {
    current_module = m;
    if context == IMPL {
        dispatch += "\t{kidl.prefix}{m.name},\n";
        module.walk_exports(HANDLE);
        if is_dynamic {
            dynamic_dispatch += "\tif (strcmp(module, \"{m.name}\") == 0) {{ return {kidl.prefix}{m.name}_dynamic_dispatch(state, name, param_count, stack, retval); }}\n";
            println("bool {kidl.prefix}{m.name}_dynamic_dispatch(kalos_run_state* state, const char* name, int param_count, kalos_stack* stack, bool retval) {{");
        } else {
            println("void {kidl.prefix}{m.name}(kalos_run_state* state, int function, int param_count, kalos_stack* stack, bool retval) {{");
            println("\tswitch (function) {{");
        }
        in_module = true;
        module.walk_exports(DISPATCH);
        in_module = false;
        if is_dynamic {
            println("\treturn false;");
        } else {
            println("\t}}");
        }
        println("}}");
        println("");
    } else {
        module.walk_exports(FORWARD);
        #println("");
    }
}

on module.handler(context, handler) {
    if context != HANDLE {
        return;
    }
    println("// id={handler.module_index:04x}:{handler.handler_index:04x} name={handler.name}");
    if current_module.name == "builtin" {
        print("void {kidl.prefix}trigger_{handler.name}(kalos_run_state* state");
    } else {
        print("void {kidl.prefix}{current_module.name}_trigger_{handler.name}(kalos_run_state* state");
    }
    var index = 0;
    var arg;
    for arg in handler.args {
        print(", K__p{arg} a{index}");
        index += 1;
    }
    println(") {{");
    index = 0;
    for arg in handler.args {
        println("\tkalos_load_arg_{arg}(state, {index}, a{index});");
        index += 1;
    }
    println("\tkalos_trigger(state, kalos_make_address({handler.module_index:#x}, {handler.handler_index:#x}));");
    println("}}");
    println("");
}

on module.object(context, object) {
    if context != HANDLE {
        return;
    }
    println("//{kidl.prefix}{current_module.name}_object_{object.name}_props");
    if is_dynamic {
        println("bool {kidl.prefix}{current_module.name}_object_{object.name}_props_fn(kalos_run_state* state, kalos_object_ref* object, const char* name, int param_count, kalos_stack* stack) {{");
    } else {
        println("bool {kidl.prefix}{current_module.name}_object_{object.name}_props_fn(kalos_run_state* state, kalos_object_ref* object, int function, int param_count, kalos_stack* stack) {{");
        println("\tswitch (function) {{");
    }
    in_object = true;
    module.walk_object_properties(OBJECT, object);
    in_object = false;
    if is_dynamic {
        println("\treturn false;");
    } else {
        println("\tdefault: return false;");
        println("\t}}");
        println("\treturn true;");
    }
    println("}}");
    println("");
    println("kalos_object_dispatch {kidl.prefix}{current_module.name}_object_{object.name}_props = {{");
    if is_dynamic {
        println("\t.dispatch_name = {kidl.prefix}{current_module.name}_object_{object.name}_props_fn");
    } else {
        println("\t.dispatch_id = {kidl.prefix}{current_module.name}_object_{object.name}_props_fn");
    }
    println("}};");
    println("");
}

on module.function(context, function) {
    if context != DISPATCH {
        return;
    }
    var arg;
    println("\t// id={function.id} name={function.name} arg_count={function.arg_count} binding={function.binding.type} {function.binding.value}");
    write_matcher(function.id, function.name, function.arg_count);
    println("\t\tLOG(\"Invoke %s\", \"{function.name}\");");
    if function.varargs == "void" {
        println("\t\tconst int arg_count = {function.arg_count};");
    } else {
        println("\t\tconst int vararg_count = kalos_stack_vararg_count(stack);");
        println("\t\tconst int arg_count = {function.arg_count + 1} + vararg_count;");
    }
    print("\t\tkalos_stack_setup_{function.arg_count}(state");
    for arg in function.args {
        print(", K__t_{arg}");
    }
    println(");");
    print("\t\t");
    if function.return_type != "void" {
        print("K__{function.return_type} x = ");
    }
    write_binding(function.binding, function.args, function.varargs);
    println(";");
    println("\t\tkalos_stack_cleanup(state, arg_count);");
    if function.return_type != "void" {
        println("\t\tif (retval) {{");
        println("\t\t\tpush_{function.return_type}(stack, x);");
        println("\t\t}} else {{");
        println("\t\t\tK__clean_{function.return_type}(stack, x);");
        println("\t\t}}");
    }
    if is_dynamic {
        println("\t\treturn true;");
    } else {
        println("\t\tbreak;");
    }
    println("\t}}");
    println("");
}

on module.property(context, property) {
    if !in_module && !in_object {
        return;
    }
    var maybe_object = "";
    if in_object {
        maybe_object = ", object";
    }
    if property.read_binding.invoke_id != 0 {
        println("\t// id={property.read_binding.invoke_id} name={property.name} binding={property.read_binding.type} {property.read_binding.value}");
        write_matcher(property.read_binding.invoke_id, property.name, 0);
        print("\t\tpush_{property.type}(stack, ");
        write_binding(property.read_binding, [], "void");
        println(");");
        if is_dynamic {
            println("\t\treturn true;");
        } else {
            println("\t\tbreak;");
        }
        println("\t}}");
    }
    if property.write_binding.invoke_id != 0 {
        println("\t// id={property.write_binding.invoke_id} name={property.name} binding={property.write_binding.type} {property.write_binding.value}");
        write_matcher(property.write_binding.invoke_id, property.name, 1);
        println("\t\tconst int arg_count = 1;");
        print("\t\t");
        write_binding(property.write_binding, [property.type], "void");
        println(";");
        println("\t\tkalos_stack_cleanup(state, 1);");
        if is_dynamic {
            println("\t\treturn true;");
        } else {
            println("\t\tbreak;");
        }
        println("\t}}");
    }
}
