import function;
import module;
import property;
import handles;
import object;

var in_object = false;
var dispatch = "";

handle open {
    println("/* Generated by kalos_idl_compiler - DO NOT MODIFY */");
    println("");
}

handle close {
    println("kalos_dispatch_fn {module.prefix}dispatch[] = {{");
    print(dispatch);
    println("}};");
}

handle module.begin(m) {
    println("void {m.prefix}{m.name}(kalos_state state, int function, kalos_stack* stack, bool retval) {{");
    println("\tswitch (function) {{");
    dispatch += "\t{m.prefix}{m.name},\n";
}

handle handle_export {
    println("// id={handles.module_index:04x}:{handles.handle_index:04x} name={handles.name}");
    if (module.name == "builtin") {
        print("void {module.prefix}trigger_{handles.name}(kalos_state state");
    } else {
        print("void {module.prefix}{module.name}_trigger_{handles.name}(kalos_state state");
    }
    var index = 0;
    var arg;
    for arg in function.args {
        print(", ");
        if (arg == "string") {
            print("kalos_string a{index}");
        } else if (arg == "number" || arg == "bool") {
            print("kalos_int a{index}");
        } else if (arg == "object") {
            print("kalos_object* a{index}");
        }
        index += 1;
    }
    println(") {{");
    index = 0;
    for arg in function.args {
        println("\tkalos_load_arg_{arg}(state, {index}, a{index});");
    }
    println("\tkalos_trigger(state, kalos_make_address({handles.module_index:#x}, {handles.handle_index:#x}));");
    println("}}");
    println("");
}

handle begin_object {
    println("//{module.prefix}{module.name}_object_{object.name}_props");
    println("bool {module.prefix}{module.name}_object_{object.name}_props(kalos_state state, kalos_object* object, int function, kalos_stack* stack) {{");
    println("\tswitch (function) {{");
    in_object = true;
}

handle object_property {
    println("//");
}

handle end_object {
    println("\tdefault: return false;");
    println("\t}}");
    println("\treturn true;");
    println("}}");
    println("");
    in_object = false;
}

#handle function(function) {
handle function_export {
    println("\t// id={function.id} name={function.name} arg_count={function.arg_count} symbol={function.realname}");
    println("\tcase {function.id}: {{");
    println("\t\tLOG(\"Invoke %s\", \"{function.name}\");");
    println("\t\tint arg_count = {function.arg_count};");
    var arg_offset = 1;
    if (function.varargs == "void") {
        println("\t\tkalos_stack_fixup_no_varargs(arg_count, stack);");
    } else {
        println("\t\tint ofs = kalos_stack_fixup_varargs(arg_count, stack);");
    }
    print("\t\t");
    if (function.return_type == "string") {
        print("kalos_string x = ");
    } else if (function.return_type == "number" || function.return_type == "bool") {
        print("kalos_int x = ");
    } else if (function.return_type == "object") {
        print("kalos_object* x = ");
    }
    var arg;
    var index = 0;
    print("{function.realname}(state");
    for arg in function.args {
        print(", ");
        if (arg == "string") {
            print("&peek(stack, {-index-arg_offset})->value.string");
        } else if (arg == "number" || arg == "bool") {
            print("peek(stack, {-index-arg_offset})->value.number");
        } else if (arg == "object") {
            print("peek(stack, {-index-arg_offset})->value.object");
        }
        index += 1;
    }
    if (function.varargs != "void") {
        print(", peek(stack, -arg_count-ofs-1)->value.number, peek(stack, -arg_count-1)");
    }
    println(");");
    if (function.varargs == "void") {
        println("\t\tkalos_stack_cleanup_no_varargs(arg_count, 0, state, stack);");
    } else {
        println("\t\tkalos_stack_cleanup_varargs(arg_count, ofs, state, stack);");
    }
    if (function.return_type != "void") {
        println("\t\tif (retval) {{");
        println("\t\t\tpush_{function.return_type}(stack, x);");
        if (function.return_type == "string") {
            println("\t\t}} else {{");
            println("\t\t\tkalos_string_release(state, x);");
        } else if (function.return_type == "object") {
            println("\t\t}} else {{");
            println("\t\t\tkalos_object_release(state, x);");
        }
        println("\t\t}}");
    }
    println("\t\tbreak;");
    println("\t}}");
    println("");
}

handle property_export {
    var maybe_object = "";
    if (in_object) {
        maybe_object = ", object";
    }
    if (property.read_id != 0) {
        println("\t// id={property.read_id} name={property.name} symbol={property.read_symbol}");
        println("\tcase {property.read_id}: {{");
        println("\t\tpush_{property.type}(stack, {property.read_symbol}(state{maybe_object}));");
        println("\t\tbreak;");
        println("\t}}");
    }
    if (property.write_id != 0) {
        println("\t// id={property.write_id} name={property.name} symbol={property.write_symbol}");
        println("\tcase {property.write_id}: {{");
        println("\t\tkalos_stack_fixup_no_varargs(1, stack);");
        print("\t\t{property.write_symbol}(state{maybe_object}, ");
        if (property.type == "string") {
            print("&peek(stack, -1)->value.string");
        } else if (property.type == "number" || property.type == "bool") {
            print("peek(stack, -1)->value.number");
        } else if (property.type == "object") {
            print("peek(stack, -1)->value.object");
        }
        println(");");
        println("\t\tkalos_stack_cleanup_no_varargs(1, 0, state, stack);");
        println("\t\tbreak;");
        println("\t}}");
    }
}

handle module.end {
    println("\t}}");
    println("}}");
    println("");
}
