import module;
import kidl;

var in_object = false;
var in_module = false;
var is_dynamic = false;
var dispatch = "";
var dynamic_dispatch = "";
const FORWARD = "forward";  # Forward declaration phase
const IMPL = "impl";        # Implementation phase
const HANDLE = "handle";
const DISPATCH = "dispatch";
const OBJECT = "object";
const TYPES = [
    ["string",  "STRING",   "kalos_string", "*",      "kalos_string_release(state, x);"],
    ["number",  "NUMBER",   "kalos_int", "",          ";"],
    ["bool",    "BOOL",     "kalos_int", "",          ";"],
    ["object",  "OBJECT",   "kalos_object_ref", "*",  "kalos_object_release(state, &x);"],
    ["any",     "ANY",      "kalos_value", "*",       "kalos_clear(state, x);"],
];

handle open(dynamic) {
    is_dynamic = dynamic;
    println("/* Generated by kalos_idl_compiler - DO NOT MODIFY */");
    println("");
    var type;
    for type in TYPES {
        println("#define K__p{type[0]:<6} {type[2]}{type[3]}");
        println("#define K__{type[0]:<7} {type[2]}");
    }
    println("");
    for type in TYPES {
        println("#define K__t_{type[0]:<6} KALOS_VALUE_{type[1]}");
    }
    println("");
    for type in TYPES {
        println("#define K__clean_{type[0]:<6}(stack, x) {type[4]}");
    }
    println("#define K__arg(n, x)     peek(stack, (n)-(x))");
    println("#define K__arg_string(x) &((x)->value.string)");
    println("#define K__arg_number(x) (x)->value.number");
    println("#define K__arg_bool(x)   (x)->value.number");
    println("#define K__arg_object(x) &((x)->value.object)");
    println("#define K__arg_any(x)    (x)");
    println("");
    kidl.walk_modules(FORWARD);
    kidl.walk_modules(IMPL);
}

handle close {
    if (is_dynamic) {
        println("bool {module.prefix}dynamic_dispatch(kalos_state state, const char* module, const char* name, int param_count, kalos_stack* stack, bool retval) {{");
        println(dynamic_dispatch);
        println("\tLOG(\"Module %s not found\", module);");
        println("\treturn false;");
        println("}}");
        println("");
    } else {
        println("kalos_dispatch_fn {module.prefix}dispatch[] = {{");
        print(dispatch);
        println("}};");
        println("");
    }
    println("#undef K__string");
    println("#undef K__number");
    println("#undef K__bool");
    println("#undef K__object");
    println("#undef K__any");
}

handle module.begin(context, m) {
    if (context == IMPL) {
        dispatch += "\t{m.prefix}{m.name},\n";
        module.walk_exports(HANDLE);
        if (is_dynamic) {
            dynamic_dispatch += "\tif (strcmp(module, \"{m.name}\") == 0) {{ return {m.prefix}{m.name}_dynamic_dispatch(state, name, param_count, stack, retval); }}\n";
            println("bool {m.prefix}{m.name}_dynamic_dispatch(kalos_state state, const char* name, int param_count, kalos_stack* stack, bool retval) {{");
        } else {
            println("void {m.prefix}{m.name}(kalos_state state, int function, int param_count, kalos_stack* stack, bool retval) {{");
            println("\tswitch (function) {{");
        }
        in_module = true;
        module.walk_exports(DISPATCH);
        in_module = false;
        if (is_dynamic) {
            println("\treturn false;");
        } else {
            println("\t}}");
        }
        println("}}");
        println("");
    } else {
        module.walk_exports(FORWARD);
        #println("");
    }
}

handle module.handle_(context, handle_) {
    if (context != HANDLE) {
        return;
    }
    println("// id={handle_.module_index:04x}:{handle_.handle_index:04x} name={handle_.name}");
    if (module.name == "builtin") {
        print("void {module.prefix}trigger_{handle_.name}(kalos_state state");
    } else {
        print("void {module.prefix}{module.name}_trigger_{handle_.name}(kalos_state state");
    }
    var index = 0;
    var arg;
    for arg in handle_.args {
        print(", K__p{arg} a{index}");
        index += 1;
    }
    println(") {{");
    index = 0;
    for arg in handle_.args {
        println("\tkalos_load_arg_{arg}(state, {index}, a{index});");
        index += 1;
    }
    println("\tkalos_trigger(state, kalos_make_address({handle_.module_index:#x}, {handle_.handle_index:#x}));");
    println("}}");
    println("");
}

handle module.object(context, object) {
    if (context != HANDLE) {
        return;
    }
    println("//{module.prefix}{module.name}_object_{object.name}_props");
    if (is_dynamic) {
        println("bool {module.prefix}{module.name}_object_{object.name}_props(kalos_state state, kalos_object_ref* object, const char* name, int param_count, kalos_stack* stack) {{");
    } else {
        println("bool {module.prefix}{module.name}_object_{object.name}_props(kalos_state state, kalos_object_ref* object, int function, int param_count, kalos_stack* stack) {{");
        println("\tswitch (function) {{");
    }
    in_object = true;
    module.walk_object_properties(OBJECT, object);
    in_object = false;
    if (is_dynamic) {
        println("\treturn false;");
    } else {
        println("\tdefault: return false;");
        println("\t}}");
        println("\treturn true;");
    }
    println("}}");
    println("");
}

handle module.function(context, function) {
    if (context != DISPATCH) {
        return;
    }
    var arg;
    println("\t// id={function.id} name={function.name} arg_count={function.arg_count} symbol={function.realname}");
    if (is_dynamic) {
        println("\tif (strcmp(name, \"{function.name}\") == 0) {{");
    } else {
        println("\tcase {function.id}: {{");
    }
    println("\t\tLOG(\"Invoke %s\", \"{function.name}\");");
    if (function.varargs == "void") {
        println("\t\tconst int arg_count = {function.arg_count};");
    } else {
        println("\t\tconst int vararg_count = kalos_stack_vararg_count(stack);");
        println("\t\tconst int arg_count = {function.arg_count + 1} + vararg_count;");
    }
    print("\t\tkalos_stack_setup_{function.arg_count}(stack");
    for arg in function.args {
        print(", K__t_{arg}");
    }
    println(");");
    print("\t\t");
    if (function.return_type != "void") {
        print("K__{function.return_type} x = ");
    }
    var index = 0;
    if (function.c) {
        var c = function.c;
        for arg in function.args {
            c = replace(c, "@" + index, "(K__arg_{arg}(K__arg(arg_count, {index+1})))");
            index += 1;
        }
        println("{c};");
    } else {
        print("{function.realname}(state");
        for arg in function.args {
            print(", K__arg_{arg}(K__arg(arg_count, {index+1}))");
            index += 1;
        }
        if (function.varargs != "void") {
            print(", vararg_count, kalos_stack_vararg_start(stack, vararg_count)");
        }
        println(");");
    }
    println("\t\tkalos_stack_cleanup(arg_count, state, stack);");
    if (function.return_type != "void") {
        println("\t\tif (retval) {{");
        println("\t\t\tpush_{function.return_type}(stack, x);");
        println("\t\t}} else {{");
        println("\t\t\tK__clean_{function.return_type}(stack, x);");
        println("\t\t}}");
    }
    if (is_dynamic) {
        println("\t\treturn true;");
    } else {
        println("\t\tbreak;");
    }
    println("\t}}");
    println("");
}

handle module.property(context, property) {
    if (!in_module && !in_object) {
        return;
    }
    var maybe_object = "";
    if (in_object) {
        maybe_object = ", object";
    }
    if (property.read_id != 0) {
        println("\t// id={property.read_id} name={property.name} symbol={property.read_symbol}");
        if (is_dynamic) {
            println("\tif (param_count == 0 && strcmp(name, \"{property.name}\") == 0) {{");
        } else {
            println("\tcase {property.read_id}: {{");
        }
        println("\t\tpush_{property.type}(stack, {property.read_symbol}(state{maybe_object}));");
        if (is_dynamic) {
            println("\t\treturn true;");
        } else {
            println("\t\tbreak;");
        }
        println("\t}}");
    }
    if (property.write_id != 0) {
        println("\t// id={property.write_id} name={property.name} symbol={property.write_symbol}");
        if (is_dynamic) {
            println("\tif (param_count == 1 && strcmp(name, \"{property.name}\") == 0) {{");
        } else {
            println("\tcase {property.write_id}: {{");
        }
        println("\t\t{property.write_symbol}(state{maybe_object}, K__arg_{property.type}(K__arg(1, {1})));");
        println("\t\tkalos_stack_cleanup(1, state, stack);");
        if (is_dynamic) {
            println("\t\treturn true;");
        } else {
            println("\t\tbreak;");
        }
        println("\t}}");
    }
}
